apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.jaredsburrows.license'

android {
    compileSdkVersion 29
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "eu.pretix.pretixprint"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 30
        versionName "2.4.0"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        disable 'InvalidPackage' // problem with jetty and bouncycastle
        disable 'MissingTranslation'
    }

    signingConfigs {
        release {
            storeFile file("../../release.keystore")
            storePassword System.getenv("KSTOREPWD")
            keyAlias "pretixdroid"
            keyPassword System.getenv("KSTOREPWD")
        }
    }

    buildTypes {
        def sentry_dsn = "";
        try {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            sentry_dsn = properties.getProperty('sentry.dsn')
            if (sentry_dsn == null) {
                sentry_dsn = "null"
            } else {
                sentry_dsn = "\"" + sentry_dsn + "\""
            }
        } catch (all) {
            sentry_dsn = "null"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "String", "SENTRY_DSN", sentry_dsn
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            buildConfigField "String", "SENTRY_DSN", "null"
        }
    }

    flavorDimensions "edition"
    productFlavors {
        foss {
            dimension "edition"
            versionNameSuffix "-foss"
        }
        full {
            dimension "edition"
            versionNameSuffix "-full"
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'org.jetbrains.anko:anko:0.10.6'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.tom_roush:pdfbox-android:1.8.10.0'
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    // libpretixprint
    implementation 'com.github.librepdf:openpdf:1.3.24'
    implementation 'com.github.andob:android-awt:1.0.0'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'com.google.zxing:core:3.3.3'

    implementation 'joda-time:joda-time:2.10.10'
    implementation 'io.sentry:sentry-android:1.7.29'
    implementation 'org.slf4j:slf4j-nop:1.7.30'

    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation(project(':cups4j')) {
        transitive = false
    }
    implementation(project(':libpretixprint')) {
        transitive = false
    }

    implementation 'com.github.anastaciocintra:escpos-coffee:4.0.2'

    // Zebra LinkOS SDK
    // Comment == Version shipped by Zebra
    fullImplementation(project(':ZSDK')) {
        transitive = false
    }
    fullImplementation 'org.bouncycastle:bcprov-jdk15on:1.65.01' // 1.48
    // fullImplementation 'commons-codec:commons-codec:1.15' // 1.15; already included for libpretixprint
    fullImplementation 'commons-io:commons-io:2.7' // 2.2
    fullImplementation 'org.apache.commons:commons-lang3:3.4' // 3.4
    // fullImplementation 'com.google.zxing:core:3.3.1' // 3.3.1; already included for libpretixprint
    fullImplementation 'dom4j:dom4j:1.6.1' // 1.6.1
    fullImplementation 'com.fasterxml.jackson.core:jackson-annotations:2.12.4' // 2.2.3
    fullImplementation 'com.fasterxml.jackson.core:jackson-core:2.12.4' // 2.2.3
    fullImplementation 'com.fasterxml.jackson.core:jackson-databind:2.12.4' // 2.2.3
    fullImplementation 'jaxen:jaxen:1.2.0' // 1.1-beta-6
    fullImplementation 'org.simpleframework:simple-xml:2.7.1' // 2.7.1
    fullImplementation 'org.snmp4j:snmp4j:3.4.4' // 2.3.0

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.12.4"

    implementation 'net.sourceforge.streamsupport:streamsupport-cfuture:1.7.3'
}
repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}
kapt {
    generateStubs = true
}
