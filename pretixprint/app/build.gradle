plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.kapt"
    id "com.jaredsburrows.license"
    id "io.sentry.android.gradle"
}

android {
    namespace 'eu.pretix.pretixprint'

    compileSdk 34

    defaultConfig {
        applicationId "eu.pretix.pretixprint"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 77
        versionName "2.19.8-beta1"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }

    buildFeatures {
        buildConfig = true
        viewBinding = true
        dataBinding = true
    }

    dataBinding {
        enabled = true
    }


    signingConfigs {
        release {
            storeFile file("../../release.keystore")
            storePassword System.getenv("KSTOREPWD")
            keyAlias "pretixdroid"
            keyPassword System.getenv("KSTOREPWD")
        }
    }

    buildTypes {
        def sentry_dsn = ""
        try {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            sentry_dsn = properties.getProperty('sentry.dsn')
            if (sentry_dsn == null) {
                sentry_dsn = ""
            }
        } catch (all) {
            sentry_dsn = ""
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            multiDexKeepProguard file('multidex-config.pro')
            signingConfig signingConfigs.release
            manifestPlaceholders["SENTRY_DSN"] = sentry_dsn
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            manifestPlaceholders["SENTRY_DSN"] = ""
        }
    }

    sentry {
        uploadNativeSymbols = false
        includeNativeSources = false
        includeProguardMapping = false
        autoUploadProguardMapping = false
        telemetry = false

        tracingInstrumentation {
            enabled = false
        }

        autoInstallation {
            enabled = false
        }
    }

    flavorDimensions "edition"
    productFlavors {
        foss {
            dimension "edition"
            versionNameSuffix "-foss"
        }
        full {
            dimension "edition"
            versionNameSuffix "-full"
        }
    }
    packagingOptions {
        resources {
            excludes += [
                'META-INF/DEPENDENCIES',
                'META-INF/LICENSE',
                'META-INF/LICENSE.txt',
                'META-INF/license.txt',
                'META-INF/NOTICE',
                'META-INF/NOTICE.txt',
                'META-INF/notice.txt',
                'META-INF/ASL2.0'
            ]
        }
    }

    lint {
        abortOnError false
        disable 'InvalidPackage', 'MissingTranslation'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.fragment:fragment-ktx:1.5.5"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation 'com.louiscad.splitties:splitties-toast:3.0.0'
    implementation 'com.github.razir.progressbutton:progressbutton:2.1.0'

    // libpretixprint
    implementation 'com.github.librepdf:openpdf:1.3.24'
    implementation 'com.github.pretix:android-awt:1.0.4-1'
    implementation 'com.google.zxing:core:3.3.3'

    implementation 'joda-time:joda-time:2.10.10'
    implementation 'io.sentry:sentry-android:5.7.0'
    implementation 'org.slf4j:slf4j-nop:1.7.30'

    implementation(project(':cups4j')) {
        transitive = false
    }
    implementation(project(':libpretixprint')) {
        transitive = false
    }

    implementation 'com.github.anastaciocintra:escpos-coffee:4.0.2'
    implementation 'com.sunmi:printerlibrary:1.0.13'
    implementation 'com.sunmi:printerx:1.0.13'

    // Evolis SDK
    fullImplementation(project(':EvolisSDK')) {
        transitive = true
    }
    // Zebra LinkOS SDK
    // Comment == Version shipped by Zebra
    fullImplementation(project(':ZSDK')) {
        transitive = false
    }
    fullImplementation 'org.bouncycastle:bcprov-jdk15on:1.65.01' // 1.48
    fullImplementation 'commons-codec:commons-codec:1.15' // 1.15; already included for libpretixprint
    fullImplementation 'commons-io:commons-io:2.7' // 2.2
    fullImplementation 'org.apache.commons:commons-lang3:3.4' // 3.4
    // fullImplementation 'com.google.zxing:core:3.3.1' // 3.3.1; already included for libpretixprint
    fullImplementation 'dom4j:dom4j:1.6.1' // 1.6.1
    fullImplementation 'com.fasterxml.jackson.core:jackson-annotations:2.12.4' // 2.2.3
    fullImplementation 'com.fasterxml.jackson.core:jackson-core:2.12.4' // 2.2.3
    fullImplementation 'com.fasterxml.jackson.core:jackson-databind:2.12.4' // 2.2.3
    fullImplementation 'jaxen:jaxen:1.2.0' // 1.1-beta-6
    // fullImplementation 'org.simpleframework:simple-xml:2.7.1' // 2.7.1; depends on xpp3, fails with DuplicatePlatformClasses
    fullImplementation 'org.snmp4j:snmp4j:3.4.4' // 2.3.0

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.12.4"

    implementation 'net.sourceforge.streamsupport:streamsupport-cfuture:1.7.3'

    implementation 'com.facebook.soloader:soloader:0.10.4'
    debugImplementation 'com.facebook.soloader:soloader:0.10.4'
    debugImplementation 'com.facebook.flipper:flipper:0.183.0'
    debugImplementation 'com.facebook.flipper:flipper-network-plugin:0.183.0'
    releaseImplementation 'com.facebook.flipper:flipper-noop:0.183.0'
}
repositories {
    google()
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}
