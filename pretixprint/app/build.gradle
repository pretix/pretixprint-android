apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "eu.pretix.pretixprint"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 20
        versionName "2.1.0"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        disable 'InvalidPackage' // problem with jetty and bouncycastle
        disable 'MissingTranslation'
    }

    signingConfigs {
        release {
            storeFile file("../../release.keystore")
            storePassword System.getenv("KSTOREPWD")
            keyAlias "pretixdroid"
            keyPassword System.getenv("KSTOREPWD")
        }
    }

    buildTypes {
        def sentry_dsn = "";
        try {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            sentry_dsn = properties.getProperty('sentry.dsn')
            if (sentry_dsn == null) {
                sentry_dsn = "null"
            } else {
                sentry_dsn = "\"" + sentry_dsn + "\""
            }
        } catch (all) {
            sentry_dsn = "null"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "String", "SENTRY_DSN", sentry_dsn
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            buildConfigField "String", "SENTRY_DSN", "null"
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'org.jetbrains.anko:anko:0.10.6'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.tom_roush:pdfbox-android:1.8.10.0'
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    // libpretixprint
    implementation 'com.itextpdf:itextg:5.5.10'
    implementation 'com.google.zxing:core:3.3.3'

    implementation 'joda-time:joda-time:2.10.5'
    implementation 'com.joshdholtz.sentry:sentry-android:1.6.0'
    implementation 'io.sentry:sentry-android:1.7.29'
    implementation 'org.slf4j:slf4j-nop:1.7.30'

    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation(project(':cups4j')) {
        transitive = false
    }
    implementation(project(':libpretixprint')) {
        transitive = false
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.10.1"

    implementation 'net.sourceforge.streamsupport:streamsupport-cfuture:1.7.1'
}
repositories {
    mavenCentral()
}
kapt {
    generateStubs = true
}
